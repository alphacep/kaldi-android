buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
    }
}

def archiveName = "vosk-android"
def libVersion = "0.3.17"

allprojects {
  repositories {
      google()
      jcenter()
  }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 6
        versionName = libVersion
        archivesBaseName = archiveName
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    publications {
        aar(MavenPublication) {
            groupId 'com.alphacep'
            artifactId archiveName
            version libVersion
            artifact("$buildDir/outputs/aar/$archiveName-release.aar")
        }
    }
}

task kaldi(type: Exec) {
    commandLine './build-kaldi.sh'
    environment ANDROID_SDK_HOME: android.getSdkDirectory()
}

dependencies {
    implementation 'net.java.dev.jna:jna:4.4.0@aar'
}

//preBuild.dependsOn kaldi
